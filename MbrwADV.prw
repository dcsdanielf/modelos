#include "protheus.ch"

User Function MBrwSA1()

    Local cAlias := "SA1"
    Local aCores := {}
    Local cFiltra := "A1_FILIAL == '" + xFilial('SA1') + "' .And. A1_EST == 'SP' "

    Private cCadastro := "Cadastro de Clientes"
    Private aRotina := {}

// op√ß√µes de filtro utilizando a FilBrowse
    Private aIndexSA1 := {}
    Private bFiltraBrw:= { || FilBrowse(cAlias,@aIndexSA1,@cFiltra) }

// quando a fun√ß√£o FilBrowse for utilizada a fun√ß√£o de pesquisa dever√° ser a PesqBrw ao inv√©s da AxPesqui

    AADD(aRotina,{"Pesquisar" ,"PesqBrw" ,0,1})
    AADD(aRotina,{"Visualizar" ,"AxVisual",0,2})
    AADD(aRotina,{"Incluir" ,"AxInclui",0,3})
    AADD(aRotina,{"Alterar" ,"AxAltera",0,4})
    AADD(aRotina,{"Excluir" ,"U_Exclui",0,5})
    AADD(aRotina,{"Legenda" ,"U_BLegenda" ,0,3})

// inclui as configura√ß√µes da legenda

    AADD(aCores,{"A1_TIPO == 'F'" ,"BR_VERDE" })
    AADD(aCores,{"A1_TIPO == 'L'" ,"BR_AMARELO" })
    AADD(aCores,{"A1_TIPO == 'R'" ,"BR_LARANJA" })
    AADD(aCores,{"A1_TIPO == 'S'" ,"BR_MARRON" })
    AADD(aCores,{"A1_TIPO == 'X'" ,"BR_AZUL" })

    dbSelectArea(cAlias)
    dbSetOrder(1)

// Cria o filtro na MBrowse utilizando a fun√ß√£o FilBrowse

    Eval(bFiltraBrw)
    dbSelectArea(cAlias)
    dbGoTop()

    mBrowse(,,,,cAlias, , , , , , aCores)
// Deleta o filtro utilizado na fun√ß√£o FilBrowse

EndFilBrw(cAlias,aIndexSA1)

Return Nil

//========================================================================================

// Exemplo: Determinando a op√ß√£o do aRotina pela informa√ß√£o recebida em nOpc
User Function Exclui(cAlias, nReg, nOpc)

    Local nOpcao := 0

    nOpcao := AxDeleta(cAlias,nReg,nOpc)

    If nOpcao == 2
//Se confirmou a exclus√£o	
        MsgInfo("Exclus√£o realizada com sucesso!")
    ElseIf nOpcao == 1
        MsgInfo("Exclus√£o cancelada!")
    Endif

Return Nil


//========================================================================================
//+-------------------------------------------
//|Fun√ß√£o: BLegenda - Rotina de Legenda
//+-------------------------------------------
User Function BLegenda()

    Local aLegenda := {}

    AADD(aLegenda,{"BR_VERDE" ,"Cons.Final" })
    AADD(aLegenda,{"BR_AMARELO" ,"Produtor Rural" })
    AADD(aLegenda,{"BR_LARANJA" ,"Revendedor" })
    AADD(aLegenda,{"BR_MARRON" ,"Solidario" })
    AADD(aLegenda,{"BR_AZUL" ,"ExportaÁ„o" })
    
    BrwLegenda(cCadastro, "Legenda", aLegenda)

Return Nil